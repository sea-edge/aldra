import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id('org.springframework.boot').version('3.4.0')
    id('com.diffplug.spotless').version('7.0.0.BETA4')
    id('org.openapi.generator').version('7.10.0')
}

apply from: "${rootDir}/gradle/common-dependencies/build.gradle"
dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web:3.4.0')
    implementation('org.springframework.boot:spring-boot-starter-actuator:3.4.0')
    implementation('org.springframework.boot:spring-boot-starter-security:3.4.0')
    implementation('org.springframework:spring-tx:6.2.0')
    implementation('io.swagger.core.v3:swagger-annotations:2.2.26')
    // FIXME https://github.com/OpenAPITools/openapi-generator/issues/12603
    implementation('jakarta.validation:jakarta.validation-api:3.1.0')
    implementation(project(':common'))
    implementation(project(':database'))
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', "${buildDir}/openapi/gen-src/main/java"]
        }
    }
}

spotless {
    encoding('UTF-8')
    java {
        targetExclude('build/openapi/gen-src/main/java/**')
        indentWithSpaces()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        googleJavaFormat()
    }
}

// see. https://openapi-generator.tech/docs/plugins#gradle
task oaGenerate(type: GenerateTask) {
    generatorName = 'spring'
    inputSpec = "${projectDir}/openapi/index.yml"
    outputDir = "${buildDir}/openapi"
    typeMappings = [
        "string+local-date": "LocalDate",
        "string+local-datetime" : "LocalDateTime"
    ]
    importMappings = [
        "LocalDate": "java.time.LocalDate",
        "LocalDateTime": "java.time.LocalDateTime"
    ]
    configOptions = [
        "hideGenerationTimestamp": "true",
        "sourceFolder": "gen-src/main/java",
        "basePackage": "aldra.api",
        "apiPackage": "aldra.api.adapter.web.controller",
        "modelPackage": "aldra.api.adapter.web.dto",
        "skipDefaultInterface": "true",
        "oas3": "true",
        "dateLibrary": "java8",
        "delegatePattern": "false",
        "interfaceOnly": "true",
        "openApiNullable": "false",
        "useTags": "true",
        "disallowAdditionalPropertiesIfNotPresent": "false",
        "useBeanValidation": "false",
    ]
}

bootRun {
    doFirst {
        file("${rootDir}/.envrc").readLines().each {
            def (key, value) = it.split(" ")[1].tokenize("=")
            environment(key, value)
        }
    }
}
